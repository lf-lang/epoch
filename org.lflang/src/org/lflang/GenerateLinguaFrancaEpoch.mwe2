module org.lflang.GenerateLinguaFrancaEpoch

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var coreProject = "org.lflang"
var lfSource = "lingua-franca/org.lflang/src"

Workflow {
    component = XtextGenerator {
        configuration = {
            project = StandardProjectConfig {
                baseName = coreProject
                rootPath = rootPath
                runtime = {
                    enabled = true
                    name = coreProject // generate core project
                    src = lfSource
                }
                genericIde = {
                    enabled = true
                    name = coreProject // add into the core project
                    src = lfSource
                }
                runtimeTest = {
                    enabled = false
                    name = "org.lflang.tests" // generate into separate test project
                }
                // Only generate Eclipse infrastructure for the Epoch build (separate repository).
                createEclipseMetaData = true
                eclipsePlugin = {
                    enabled = true
                }
                eclipsePluginTest = {
                    enabled = true
                }
            }
            code = {
                encoding = "UTF-8"
                lineDelimiter = "\n"
                fileHeader = "/*\n * generated by Xtext \${version}\n */"
                preferXtendStubs = false
            }
        }
        language = StandardLanguage {
            name = "org.lflang.LinguaFranca"
            fileExtensions = "lf"
            serializer = {
                generateStub = false
            }
            validator = {
                composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
                //composedCheck = "org.eclipse.xtext.validation.ImportUriValidator"
                // Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
                generateDeprecationValidation = true
                generateXtendStub = false
            }
            junitSupport = {
                junitVersion = "5"
                generateXtendStub = false
            }
            parserGenerator = {
                debugGrammar = true
            }
            generator = {
                generateXtendMain = false
            }
            formatter = {
                generateStub = true
            }
            projectWizard = {
                generate = false
            }
        }
    }
}
